plugins {
    id 'application'
  	id 'eclipse'
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id "org.beryx.jlink" version "2.25.0"
}

plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
    }
}

allprojects {
	repositories {
        mavenCentral()
		maven { url 'https://jitpack.io' }
	}
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javafx {
    version = "19"
    modules = [ 'javafx.graphics', 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    implementation group: 'org.openjfx', name: 'javafx-controls', version: '19'
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: '19'
    implementation group: 'com.github.strikerx3', name: 'jxinput', version: '1.0.0'
}

application {
	mainModule.set("com.markwebert.javafxapp")
    // Define the main class for the application.
    mainClass.set("com.markwebert.javafxapp.App")
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Swerve Test Viewer'
    }
    jpackage {
        jpackageHome = System.properties['java.home']
        //jpackageHome = project.properties['org.gradle.java.home']
        outputDir = 'generated-images-and-installers'
        imageName = 'Swerve Test Viewer'
        imageOptions = ['--icon', 'src/main/resources/images/icon.ico']
        skipInstaller = false
        installerName = 'Swerve Test Viewer'
        appVersion = '1.5.0'
        installerType = 'exe'
        installerOptions = ['--win-dir-chooser', '--win-menu', '--win-upgrade-uuid', 'eff3ff3d-21da-46ad-b36c-bd3a6777dadb']
    }
}